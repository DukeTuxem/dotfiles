"           |  \
"  __     __ \▓▓______ ____   ______   _______
" |  \   /  \  \      \    \ /      \ /       \
"  \▓▓\ /  ▓▓ ▓▓ ▓▓▓▓▓▓\▓▓▓▓\  ▓▓▓▓▓▓\  ▓▓▓▓▓▓▓
"   \▓▓\  ▓▓| ▓▓ ▓▓ | ▓▓ | ▓▓ ▓▓   \▓▓ ▓▓
"    \▓▓ ▓▓ | ▓▓ ▓▓ | ▓▓ | ▓▓ ▓▓     | ▓▓_____
"     \▓▓▓  | ▓▓ ▓▓ | ▓▓ | ▓▓ ▓▓      \▓▓     \
"      \▓    \▓▓\▓▓  \▓▓  \▓▓\▓▓       \▓▓▓▓▓▓▓


" Always use the XDG Base Directory specification {{{

" Refuse to run if there are some missing environment variables set
" (for plugin manager auto install essentially)
if !exists('$XDG_CONFIG_HOME') ||
	\ !exists('$XDG_DATA_HOME') ||
	\ !exists('$XDG_CACHE_HOME')
	echoerr "One or all XDG_ environment variables are not defined!"
		\ "Vim will exit..."
	exit
endif

" This is copied from: https://wiki.archlinux.org/title/XDG_Base_Directory

" :h set-args (^=)
set runtimepath^=$XDG_CONFIG_HOME/vim
set runtimepath+=$XDG_DATA_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after

set packpath^=$XDG_DATA_HOME/vim,$XDG_CONFIG_HOME/vim
set packpath+=$XDG_CONFIG_HOME/vim/after,$XDG_DATA_HOME/vim/after

let g:netrw_home = $XDG_DATA_HOME."/vim"
call mkdir($XDG_DATA_HOME."/vim/spell", 'p')

set backupdir=$XDG_CACHE_HOME/vim/backup | call mkdir(&backupdir, 'p')
set directory=$XDG_CACHE_HOME/vim/swap   | call mkdir(&directory, 'p')
set undodir=$XDG_CACHE_HOME/vim/undo     | call mkdir(&undodir,   'p')
set viewdir=$XDG_STATE_HOME/vim/view     | call mkdir(&viewdir,   'p')

call mkdir($XDG_STATE_HOME."/vim", 'p')
if !has('nvim') | set viminfofile=$XDG_STATE_HOME/vim/viminfo | endif
" }}}
" Plugins {{{

" Autoinstall the plugin manager if missing
if empty(glob('$XDG_CONFIG_HOME/vim/autoload/plug.vim'))
  silent !curl -fLo $XDG_CONFIG_HOME/vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd! VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('$XDG_CONFIG_HOME/vim/.vim-plug')

Plug 'Yggdroot/indentLine'	" show indent levels of spaces
Plug 'preservim/tagbar'         " outline symbols (requires ctags)
Plug 'junegunn/fzf'

" Git
Plug 'airblade/vim-gitgutter'   " Actual modifications inside the buffer
Plug 'tpope/vim-fugitive'       " Stage current file from vim, restore...

" motions
Plug 'tpope/vim-commentary'	" gcc / gcap / ...
Plug 'tpope/vim-surround'	" cs([
Plug 'wellle/targets.vim'	" di(

Plug 'tpope/vim-obsession'      " nice to create sessions

Plug 'tpope/vim-repeat'

" Plug 'preservim/NERDTree'

Plug 'vimwiki/vimwiki'

Plug 'junegunn/vim-easy-align'


" ToImplement
Plug 'itchyny/lightline.vim'	" lighter status bar than airline
" Plug 'tpope/vim-vinegar'

" themes
Plug 'morhetz/gruvbox'
Plug 'chriskempson/base16-vim'	" cool to test completion
Plug 'joshdick/onedark.vim'	" kind of fallback...
Plug 'karoliskoncevicius/oldbook-vim'
Plug 'sainnhe/everforest'
call plug#end()

" run :PlugInstall if there are some missing plugins
autocmd! VimEnter * if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
	\ | PlugInstall --sync | source $MYVIMRC | endif
" }}}

let mapleader = "\\"

" User Interface (View) {{{

" Terminal: (parent process)
set title
set titlestring=VIM

" Appearance:
set number		" show the line numbers on the left side...
set relativenumber	" ...with the numbers relative to the current line
set colorcolumn=80	" show a limit with a vertical mark bar
set conceallevel=0	" never replace any symbol like lambda sign, whatever
set nowrap		" allow long lines to extend as far as they go

set lazyredraw		" faster execution of macros, mappings, etc...

set belloff=all		" no bell
set novisualbell	" disable sound
set vb t_vb=		" clearing the visual bell length deactivates flashing

" Cursor: Keep centered when...
" scrolling up/down
nnoremap <expr> <C-d>    line('.') == 1 ? 'M' : '<C-d>zzzv'
nnoremap <C-u>  <C-u>zzzv
" going to the next/prev location
nnoremap <C-i>  <C-i>zzzv
nnoremap <C-o>  <C-o>zzzv
" going to a tag (land in a centered window)
nnoremap <C-]> <C-]>zzzv
" searching
nnoremap n nzzzv
nnoremap N Nzzzv
" joining
nnoremap J mzJ`z
" scrolling horizontally
nnoremap zl zL
nnoremap zh zH
" TODO: Be sure that this centers screen when jumping to marks
nnoremap <expr> ' "'" . nr2char(getchar()) . "zz"
nnoremap <expr> ` "`" . nr2char(getchar()) . "zz"
" TODO: when searching

" when :set wrap, access the visually down/up line
nnoremap j gj
nnoremap k gk

" Splits:
" sane default behavior
set splitbelow
set splitright

" create quick splits (TODO: replace with tmux ?)
nnoremap <silent> <leader>s :split<CR>
nnoremap <silent> <leader>v :vsplit<CR>

" navigation shortcuts
" (TODO: do not break vimwiki's Backspace functionnality)
nnoremap <C-h>		<C-w>h
nnoremap <C-j>		<C-w>j
nnoremap <C-k>		<C-w>k
nnoremap <C-l>		<C-w>l
" my workflow is now mature, is this ever used again ?
" how about tmux ?
"nnoremap <C-Up>		<C-w>k
"nnoremap <C-Left>	<C-w>h
"nnoremap <C-Down>	<C-w>j
"nnoremap <C-Right>	<C-w>l

" non layout destructive :bd
nnoremap <silent> <leader>d :b#<bar>bw!#<CR>
" window/tab destructive :bd
nnoremap <silent> <leader>D :bd<CR>
" shortcut of the shortcut <C-w>c
nnoremap <silent> <leader>c :close<CR>

" Zoom without <C-w><C-o> (create a new 'temporary' window) - awesome trick
nnoremap <silent> <leader>t :-tab split<CR>
" Then close with <leader>c

" Custom:
nnoremap <silent> <leader>l :ls<CR>
nnoremap <silent> <leader>L :ls!<CR>
nnoremap <silent> <leader>q :bprev<CR>
nnoremap <silent> <leader>p :bnext<CR>
nnoremap <leader>e :enew<CR>

" <plugin> mark indentation levels
let g:indentLine_char = '┊'
let g:indentLine_setColors = 1
" }}}
" Editing / Background {{{

set hidden		" allow background buffers
set swapfile		" warn/recover the same opened file multiple times
set tags=tags		" default file name to look for tags
set noautochdir		" autochdir changes the global directory, not win/tab

set mouse=a		" use mouse for all vim modes (normal/visual...)
set backspace=indent,eol,start	" erase back even in insert mode

set textwidth=79	" do break text with a newline after n typped chars
set nojoinspaces	" leave only one space when joining (gqg)
set showmatch		" briefly highlight the matching ( { [ if any

" Files:
set encoding=UTF-8
set autoread		" reload files changed outside vim
augroup onFocusAttemptToTriggerAutoread
	autocmd!
	" note: this is still not close to real-time
	autocmd FocusGained,BufEnter * :checktime
augroup END

set modeline		" enable <fileCommentChar> vim: fdm=marker
set modelines=1		" limit lines to read

" Tabs:			(Historical reason to choose 8 as a default)
set tabstop=8		" number of spaces that a <Tab> in a file counts for
set shiftwidth=0	" number of spaces to use for each step of (auto)indent
set softtabstop=0	" do not mix space and tabs to make up a set spacing
set noexpandtab		" never replace tabs with space

" Search:
set hlsearch		" highlight searches by default
set incsearch		" find the next match as we type the search
set ignorecase		" ignore case when searching...
set smartcase		" ...unless we type an uppercase letter

" search for the currently selected text in the file
vnoremap / y/\V<C-R>=escape(@",'/\')<CR><CR>

" shortcut to start a replace from the current line
nnoremap S :.,$s//gc<Left><Left><Left>
" and vim-cool is used to remove highlighted searches automatically (no :nohl)

" okay, I am done searching
nnoremap <silent><CR> :nohl<CR>

" TODO: 1 of N matches while iterating the search results and combine:
" always start a search from the cursor position
" this will loose the 'incsearch' feature.
" not incremental :(
" nnoremap / :.,$/

" should be to replace a selection (is there simpler ?)
" function! GetVisualSelection()
"   " Why is this not a built-in Vim script function?!
"   let [line_start, column_start] = getpos("'<")[1:2]
"   let [line_end, column_end] = getpos("'>")[1:2]
"   let lines = getline(line_start, line_end)
"   if len(lines) == 0
"     return ''
"   endif
"   let lines[-1] = lines[-1][: column_end - (&selection == 'inclusive' ? 1 : 2)]
"   let lines[0] = lines[0][column_start - 1:]
"   let arf = join(lines, "\n")
"   exe ':%s/'.arf."/gc<Left><Left><Left>"
" endfunction

" Completion_menu:
set wildmenu	" do not spam TAB in command line
set wildmode=list:longest,list:lastused,list:full
set wildignore=*.jpg,*.png,*.gif,*.pdf,*.pyc,*.flv,*.img
set wildchar=<Tab>	" explicit setting: default in vim, <C-e> in Vi
set wildcharm=<Tab>	" recognize tab as completer command in mappings

" Folds:
set foldenable
set foldmethod=marker
set foldlevelstart=0	" close most folds when opening a file
set foldnestmax=2	" maximum number of folds inside folds
set foldopen-=block	" do not open ( { [ folds when 'h/j/k/l' on them
" set foldclose=all	" auto close fold when leaving the fold scope

" TODO: apply this to anything ?
" C/C++ functions and so on
" custom fold expression function
function! MyShFoldExpr(lnum)
    " Get the current line
    let line = getline(a:lnum)
    " Check for block start
    if line =~ '^{\s*$'
        " echom "Start fold at line ".a:lnum
        return '>1'
    " Check for block end
    elseif line =~ '^\s*}$'
        " echom "End fold at line ".a:lnum
        return '<1'
    " Inside a fold
    else
        return '=1'
    endif
endfunction
augroup sh_folding
  autocmd!
  autocmd FileType sh setlocal foldmethod=expr
  autocmd FileType sh setlocal foldexpr=MyShFoldExpr(v:lnum)
augroup END

" Vim:
" turn off increase/decrease numbers to avoid mistakes, prefer :h v_g_CTRL-a
map <C-a> <Nop>
map <C-x> <Nop>
" free up one mapping
map + <Nop>

" consistent mapping from other vim basic motions
nnoremap Y y$

" ignore a text to replace from being recorded into registers
nnoremap c "_c

" TOFIX: is this working ? perform dot commands over visual blocks:
vnoremap . :normal .<CR>

augroup onVimEnterRestoreSessionIfAny
	autocmd!
	function! RestoreSess()
		" [Gvim specific]
		" do not restore the session if v:servername contains numbers.
		" if so, it means an existing vim instance is running, but the
		" goal is to have a fresh one started, not a mirrored instance
		if filereadable(expand("~/Session.vim")) && v:servername !~ '\d'
			source ~/Session.vim
		endif
	endfunction

	" TODO: document precisely why this was needed
	" :h automcmd-nested
	autocmd VimEnter * nested call RestoreSess()
augroup END

" Tell vim to remember certain things when we exit
"  '10   :  marks will be remembered for up to 10 previously edited files
"  "1000 :  will save up to 1000 lines for each register
"  :20   :  up to 20 lines of command-line history will be remembered
"  %     :  saves and restores the buffer list | off - (use sessions if needed)
"  h     :  disable the effect of 'hlsearch'
"  n...  :  where to save the viminfo files | viminfofile set above
set viminfo='10,\"1000,:20,h
" }}}
" Color {{{

" use truecolor (24bit-mode)
set termguicolors

" was this a fix to enable color ?
"if &term =~ "^st"
"	" from :help xterm-true-color
"	let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
"	let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
"	" Fixes mouse scrolling in st. Update: it seems fixed
"	"set ttymouse=sgr
"endif

colorscheme my-eighties
" }}}
" Command Line and :term {{{

" remap colon to semicolon, game changer...
nnoremap ; :
nnoremap : ;
vnoremap ; :
vnoremap : ;

" arrow up/down in :cmdline
cnoremap <C-k> <Up>
cnoremap <C-j> <Down>

" open help on the right
cnoreabbrev h vert h
" TODO: 'K' key behavior to open the same way as above

" Terminal: The builtin :term inside of vim (will shift in favor of tmux)
" rebind escape to avoid <C-w><S-N>
" (or was it C-n like in the following mapping) ?
tnoremap <C-[> <c-\><c-n>
" impossible on MS-Windows -> :h scroll-mouse-wheel
tnoremap <ScrollWheelUp> <C-\><C-n><C-u>
tnoremap <ScrollWheelDown> <C-d>
" }}}
" Statusline {{{

set showcmd		" show the command being typed. Ex: "a
set noruler		" deactivate the vim ruler (x:y) - handled in plugin

"-- <plugin> lightline settings
set noshowmode		" avoid native vim --INSERT-- message below lightline
set laststatus=2	" always show the status line
let g:lightline = {
\ 'colorscheme': 'darcula',
\ }
" }}}
" Function keys {{{
"
" F1 : Help
nnoremap <F2> :set wrap!<CR>
" TODO: " during the paste can this action happen automatically ?
set pastetoggle=<F3>
nnoremap <F4> :set list!<CR>

" inspect current element vim symbol name (for syntax checking)
nnoremap <F12> :call g:SynStack()<CR>
" }}}
" Language specific {{{

" Perl:
" undocumented settings to basically close all the pods but not the subs
let perl_fold=1
let perl_nofold_subs=1
let g:perldoc_split_modifier = 'rightbelow 15'
"}}}
" netrw {{{

let g:netrw_fastbrowse = 0		" always re-read directories content
let g:netrw_banner = 0			" remove header
let g:netrw_liststyle = 3		" tree like listing
let g:netrw_browse_split = 4		" open in previous buffer
" let g:netrw_winsize = 30		"uses a percentage, which we don't want
let g:netrw_list_hide = &wildignore
let g:netrw_bufsettings = 'noma nomod nu nobl nowrap ro'
" TODO: Open on selected file if any ?
nnoremap <silent> - :20Lexplore<CR>

" https://www.reddit.com/r/neovim/comments/jr5zdb/remapping_keys_in_netrw/
augroup netrw_mapping
  autocmd!
  autocmd filetype netrw call NetrwMapping()
augroup END

function! NetrwMapping()
  " close netrw the same way you opened it
  nnoremap <buffer><silent> - :wincmd p <BAR> Lexplore<CR>
  " do not replace my current 'text file' buffer with netrw content
  nnoremap <buffer> <C-l> <C-W>l

  " go above and below a directory
  " TODO: better 'h' user experience
  nnoremap <buffer> h <Plug>NetrwBrowseUpDir
  nnoremap <buffer> l <Plug>NetrwLocalBrowseCheck<BAR>zz
  " enter opens the selected file so closes the tree view to focus
  nnoremap <silent><buffer> <CR> <Plug>NetrwLocalBrowseCheck<BAR>:Lexplore<CR>
endfunction
" }}}

" Plugins config {{{

" GitGutter:
nmap <leader>Hp  <Plug>(GitGutterPreviewHunk)
nmap <leader>Hu  <Plug>(GitGutterUndoHunk)
nmap <leader>Hs  <Plug>(GitGutterStageHunk)
xmap <leader>Hs  <Plug>(GitGutterStageHunk)

" FuzzyFinding:
nnoremap <leader>f :FZF<CR>

" Vimwiki:
let g:vimwiki_list = [{'path': '~/documents/vimwiki',
			\ 'syntax': 'markdown', 'ext': '.md'}]
" let g:vimwiki_list = [{
" 	\ 'path': '~/documents/vimwiki',
" 	\ 'template_path': '~/documents/vimwiki/templates/',
" 	\ 'template_default': 'default',
" 	\ 'syntax': 'markdown',
" 	\ 'ext': '.md',
" 	\ 'path_html': '~/documents/wiki_extract',
" 	\ 'custom_wiki2html': 'vimwiki_markdown',
" 	\ 'template_ext': '.tpl'}]

" [terminal] this conflicts with the shortcut <C-W>H mapping
" not a big deal since I am often dealing with vimwiki in a separate tab
augroup onVimwikiBindShiftBackspace
	autocmd!
	autocmd FileType vimwiki nnoremap <silent><buffer> <C-H> :VimwikiGoBackLink<CR>
augroup END

" Todo: a cursor plugin ? fix the <C-o> which does not remember cursor
" automatically restore the previous cursor position
" keywords: - vim.fandom.com
"           - restore_cursor_to_file_position_in_previous_editing_session
function! ResCur()
  if line("'\"") > 1 && line("'\"") <= line("$")
    silent normal! g`"zozz
    return 1
  endif
endfunction

augroup resCur
  autocmd!
  autocmd BufWinEnter * call ResCur()
augroup END

" }}}

" Vimrc:
" quick edit vimrc in a new tab
nnoremap <silent> <leader>V :tabnew $MYVIMRC<CR>zozz
" check for any mistake in this file as soon as it is written
augroup sourceVimrcOnSave
	autocmd!
	autocmd BufWritePost * nested if expand($MYVIMRC) ==# expand('%:p')
				\ | source $MYVIMRC
				\ | echo ".vimrc saved and reloaded!"
				\ | endif
augroup END
